/*
 * Copyright Â© 2017-2018 The Charles Stark Draper Laboratory, Inc. and/or Dover Microsystems, Inc.
 * All rights reserved. 
 *
 * Use and disclosure subject to the following license. 
 *
 * Permission is hereby granted, free of charge, to any person obtaining
 * a copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 * 
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
 * LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 * OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
 * WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
module osv.password:

/* Password protection policy
 * - Prevents the leakage of passwords stored in memory
 * - Tracks taint of password memory
 * - Fails if data tainted by password memory reaches the UART
 */

import:
  osv.riscv


metadata:
  Password,
  Public


policy:
  passwordPol =
  
    // Explicit failure for writing password out
    storeGrp( mem == [+Public], addr == _, val == [+Password], env == _
            -> fail "Password tried to escape server" )

    // Public memory is always public
    ^ storeGrp(   mem == [+Public], addr == _, val == _, env == _ -> env = env, mem = mem)

    // Set password protection
    ^ storeGrp(mem == _, addr == _, val == [+Password], env == _ -> env = env, mem = val)

    // Propogate password protection
    ^ arithGrp(env == _, op1 == [+Password], op2 == _ -> env = env, res = op1)
    ^ arithGrp(env == _, op1 == _, op2 == [+Password] -> env = env, res = op2)
    ^ mulDivRemGrp(code == _, env == _, op1 == [+Password], op2 == _ -> env = env, res = op1)
    ^ mulDivRemGrp(code == _, env == _, op1 == _, op2 == [+Password] -> env = env, res = op2)
    ^ immArithGrp(env == _, op1 == [+Password] -> env = env, res = op1)
    ^ loadGrp(mem == [+Password], addr == _, env == _ -> env = env, res = mem )
    ^ storeGrp(mem == [+Password], addr == _, val == _, env == _ -> env = env, mem = mem ) 

    // Allow everything else
    ^ branchGrp(code == _, env == _, op1 == _, op2 == _ -> env = env )
    ^ jumpRegGrp(code == _, env == _, target == _ -> env = env , return = {})
    ^ jumpGrp(code == _, env == _ -> return = {})
    ^ loadUpperGrp(code == _, env == _ -> env = env, dest = {})
    ^ immArithGrp(code == _, env == _, op1 == _ -> env = env, res = {})
    ^ arithGrp(code == _, env == _, op1 == _, op2 == _ -> env = env, res = {})
    ^ loadGrp(code == _, env == _, addr == _, mem == _ -> env = env, res = {})
    ^ storeGrp(code == _, env == _, addr == _, val == _, mem == _ -> env = env, mem = {})
    ^ mulDivRemGrp(code == _, env == _, op1 == _, op2 == _ -> env = env, res = {})
    ^ csrGrp(code == _, env == _, op1 == _, csr == _ -> env = env, csr = {}, res = {})
    ^ csriGrp(code == _, env == _, csr == _ -> env = env, csr = {}, res = {})
    ^ privGrp(code == _, env == _ -> env = env)
    ^ systemGrp(code == _, env == _ -> env = env)
    ^ floatGrp(code == _, env == _ -> env = env)
    ^ atomicGrp(code == _, env == _ -> env = env)


require:
  init ISA.RISCV.Reg.Env                   {}
  init ISA.RISCV.Reg.Default               {}
  init ISA.RISCV.Reg.RZero                 {}
  init ISA.RISCV.CSR.Default               {}
  init ISA.RISCV.CSR.MTVEC                 {}

  init Tools.Link.MemoryMap.Default        {}
  init Tools.Link.MemoryMap.UserHeap       {}
  init Tools.Link.MemoryMap.UserStack      {}

  init SOC.IO.UART_0                       {Public}
  init SOC.IO.GEM0                         {Public}
  init SOC.Memory.Flash_0                  {}
  init SOC.Memory.Ram_0                    {}

  init SOC.IO.TEST                         {}

  init Tools.Elf.Section.SHF_ALLOC         {}
  init Tools.Elf.Section.SHF_EXECINSTR     {}
  init Tools.Elf.Section.SHF_WRITE         {}

  init webapp.var.pw_byte                  {Password}
